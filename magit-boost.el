;;; magit-boost.el --- Boost Magit performance on slow network. -*- lexical-binding: t; -*-

;; Copyright (C) 2025 Jérémy Compostella

;; Author: Jérémy Compostella <jeremy.compostella@gmail.com>
;; Created: November 2025
;; Keywords: extensions magit
;; Homepage: https://github.com/jeremy-compostella/magit-bash
;; Package-Version: 1.0
;; Package-Requires: ((emacs "29.4") (magit "4.4.0"))

;; This program is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

;;; Commentary:
;;
;; Magit Boost is an Emacs package designed to accelerate Magit's performance
;; when used over high-latency networks, such as corporate VPNs. Magit's default
;; implementation assumes fast shell command execution and file access, an
;; assumption that often doesn't hold true in these environments. This module
;; optimizes Magit by advising specific Git and Tramp functions to introduce
;; several improvements:
;; 1. Reducing the number of =git= commands by utilizing cached information or
;;    performing basic path computations.
;; 2. Batching commands related to file attributes within the Git tree.
;; 3. Implementing optimized versions of synchronous =git= command execution.

(require 'cl-extra)
(require 'cl-macs)
(require 'cl-seq)
(require 'magit)
(require 'tramp)
(require 'with-editor)

(defcustom magit-boost-buffer-name "*magit-boost*"
  "Name of the magit bash buffer."
  :type 'string)

(defcustom magit-boost-debug nil
  "When non-nil, enable debug logging for Magit Boost.

If enabled, Magit Boost will:
1. Insert the raw shell commands sent to the Bash process into the
boost buffer for inspection.
2. Prevent the boost buffer from being erased after a command
completes, allowing you to review the full history of stdout
and stderr."
  :type 'boolean)

(defvar-local magit-boost-git-dir nil
  "Buffer-local variable to cache the Git tree object for the current
repository.")

(defvar-local magit-boost-git-dir-truename nil
  "Buffer-local variable to cache the Git tree object for the current
repository.")

(defvar-local magit-boost-git-tree-files '())

(defvar-local magit-boost-cmd-state nil
  "Variable tracking the state of the current command.
This variable ensures proper serialization and state management
for asynchronous command execution within a Magit Boost buffer.

Possible values are:
- `free': No command is currently running, and the buffer is ready
   to accept a new command.
- `running': A command has been submitted to the Bash process and
   is currently being executed.
- `complete': A command has finished execution, and its output has
   been processed. The buffer is awaiting cleanup or further action
   before returning to the `'free` state.")

(defvar-local magit-boost-cmd-stdout '()
  "Variable storing the boundaries of the standard output.

This list contains two buffer positions: the start and end of the
stdout section from the last command executed in this Magit Boost
buffer.")

(defvar-local magit-boost-cmd-stderr '()
  "Variable storing the boundaries of the standard error output.

This list contains two buffer positions: the start and end of the
stderr section from the last command executed in this Magit Boost
buffer.")

(defvar-local magit-boost-cmd-ret -1
  "Variable storing the exit code of the last command executed.")

(defvar-local magit-boost-cmd-flush-file-properties nil
  "Variable indicating whether Tramp file properties should be
flushed after the current command completes.")

(defun magit-boost-cmd-init ()
  "Initialize the buffer-local command state variables."
  (setq magit-boost-cmd-state 'free
	magit-boost-cmd-stdout '()
	magit-boost-cmd-stderr '()
	magit-boost-cmd-ret -1
	magit-boost-cmd-flush-file-properties nil))

(defvar-local magit-boost-connection-type nil
  "Buffer-local variable specifying the connection type for the Magit Bash
process.

This variable determines how the Bash process is started in the current
buffer.  Its value should be either 'pty (for pseudo-terminal) or
nil (for pipe), depending on the requirements of the Git command being
executed and the contents of the buffer.

- If set to 'pty, the Bash process uses a pseudo-terminal, which is generally
required for interactive commands and for proper handling of Magit output.

- If set to nil, the Bash process uses a pipe, which may be necessary for buffers
containing carriage return characters (\\r), as pty processes can have issues
with such input.

This variable is automatically set when creating a new Magit Boost buffer via
`magit-boost-new-buffer', and is used to ensure the correct process connection
type is chosen for each Git command execution.")

(defun magit-boost-git-dir (dir)
  (with-temp-buffer
    (setq default-directory dir)
    (if (= (process-file "git" nil t nil "rev-parse" "--git-dir") 0)
	(string-trim (buffer-string))
      (error "Failed to read the git tree"))))

(defun magit-boost-create-buffer (dir buffer connection-type)
  "Create and return a new Magit Boost buffer for DIR with
CONNECTION-TYPE. If BUFFER is not nil, it clones the buffer
variables instead of determining them."
  (setf dir
	(if buffer
	    (with-current-buffer buffer
	      default-directory)
	  (with-temp-buffer
	    (setq default-directory dir)
	    (with-parsed-tramp-file-name (expand-file-name dir) nil
	      (if (= (process-file "git" nil t nil "rev-parse" "--show-toplevel") 0)
		  (tramp-make-tramp-file-name v (string-trim (buffer-string))))))))
  (unless dir
    (error "No git repository found"))
  (let* ((buf-name magit-boost-buffer-name)
	 (new-buffer (generate-new-buffer buf-name)))
    (uniquify-rationalize-file-buffer-names buf-name dir new-buffer)
    (with-current-buffer new-buffer
      (if buffer
	  (dolist (var '(default-directory
			 magit-boost-git-dir
			 magit-boost-git-tree-files
			 magit-boost-git-dir-truename))
	    (set var (with-current-buffer buffer
		       (symbol-value var))))
	(let ((git-dir (magit-boost-git-dir dir)))
	  (setq default-directory (concat dir "/")
		magit-boost-git-dir git-dir
		magit-boost-git-tree-files (list (concat default-directory git-dir))
		magit-boost-git-dir-truename (file-truename git-dir))))
      (setq magit-boost-connection-type connection-type)
      (magit-boost-cmd-init))
    new-buffer))

(defun magit-boost-in-git-dir (filename)
  (when magit-boost-git-dir
    (let ((git-dir (concat default-directory magit-boost-git-dir)))
      (or (string-prefix-p git-dir filename)
	  (string-prefix-p magit-boost-git-dir-truename filename)))))

(defun magit-boost-buffers (filename)
  "Return a list of Magit Boost buffers for FILENAME."
  (seq-filter (lambda (buffer)
		(with-current-buffer buffer
		  (and magit-boost-cmd-state
		       (or (string-prefix-p default-directory filename)
			   (magit-boost-in-git-dir filename)))))
	      (buffer-list)))

(defun magit-boost-buffer (filename connection-type &optional buffers)
  (let ((buffers (or buffers (magit-boost-buffers filename))))
    (cl-flet ((available-p (buffer)
		(with-current-buffer buffer
		  (and (eq magit-boost-cmd-state 'free)
		       (eq magit-boost-connection-type connection-type)))))
      (car (seq-filter #'available-p buffers)))))

(defmacro with-magit-boost-buffer (directory connection-type &rest body)
  (declare (indent 2))
  (let ((buffer (gensym "buffer")))
    `(progn
       (setf ,buffer (magit-boost-buffer ,directory ,connection-type))
       (when ,buffer
	 (with-current-buffer ,buffer
	   (progn ,@body))))))

(defun magit-boost-buffer-create (dir connection-type)
  "Return a Magit Boost buffer associated with DIR, creating one if necessary."
  (let* ((buffers (magit-boost-buffers dir))
	 (available (magit-boost-buffer dir connection-type buffers))
	 (buffer (or available
		     (magit-boost-create-buffer dir (unless available
						      (car buffers))
						connection-type))))
    (unless (get-buffer-process buffer)
      (let* ((process-connection-type connection-type)
	     (process (start-file-process "magit-boost-process" buffer "bash")))
	(process-send-string process "export PS1=''\n")
	(accept-process-output process 1 nil t)
	(set-process-filter process #'magit-boost-filter)))
    buffer))

(defmacro with-magit-boost-buffer-create (directory connection-type &rest body)
  (declare (indent 2))
  `(with-current-buffer (magit-boost-buffer-create ,directory ,connection-type)
     (progn ,@body)))

(defun magit-boost-submit-cmd (local-dir cmd input destination)
  (unless (eq magit-boost-cmd-state 'free)
    (error "Buffer is in unexpected %s state" magit-boost-cmd-state))
  (unless magit-boost-debug
    (erase-buffer))
  (setq magit-boost-cmd-state 'running)
  (let ((stderr-local "/tmp/magit-boost-stderr")
	(stderr-magic "MAGIT_BOOST_STDERR")
	(done-magic "MAGIT_BOOST_DONE")
	(dir local-dir))
    (cl-macrolet ((cappend (&rest args)
		    `(setf cmd (apply #'concat cmd (list ,@args))))
		  (cprepend (&rest args)
		    `(setf cmd (apply #'concat ,@args (list cmd)))))
      (when input
	(cprepend "echo '" (replace-regexp-in-string "'" "'\"'\"'" input)
		  "' | "))
      (when (and (listp destination) (stringp (cadr destination)))
	(cappend " 2>'" stderr-local "'"))
      (cappend "; export RET=$?")
      (cappend "; echo -n " stderr-magic
	       "; if [ -e '" stderr-local "' ]"
	       "; then cat '" stderr-local "'; fi")
      (cprepend "cd " dir ";")
      (cappend "; echo $RET " done-magic "; cd - > /dev/null\n"))
    (when magit-boost-debug
      (save-excursion
	(goto-char (point-max))
	(insert cmd)))
    (setq magit-boost-cmd-stdout (list (point-max)))
    (process-send-string (get-buffer-process (current-buffer)) cmd)))

(defun magit-boost-filter (process string)
  (let ((former-buffer (process-get process 'magit-boost-buffer)))
    (with-current-buffer (or former-buffer (process-buffer process))
      (goto-char (point-max))
      (insert string)
      (when (eq magit-boost-cmd-state 'running)
	(when (re-search-backward "\\([0-9]+\\) MAGIT_BOOST_DONE"
				  (car magit-boost-cmd-stdout) t)
	  (setq magit-boost-cmd-ret (string-to-number (match-string 1)))
	  (let ((end (match-beginning 0)))
	    (if (re-search-backward "MAGIT_BOOST_STDERR"
				    (car magit-boost-cmd-stdout) t)
		(progn
		  (setcdr magit-boost-cmd-stdout (list (match-beginning 0)))
		  (setq magit-boost-cmd-stderr (list (match-end 0) end)))
	      (setcdr magit-boost-cmd-stdout (list end))))
	  (setq magit-boost-cmd-state 'complete))
	(when (eq magit-boost-cmd-state 'complete)
	  (when magit-boost-cmd-flush-file-properties
	    (dolist (filename magit-boost-git-tree-files)
	      (tramp-flush-file-properties
	       (tramp-dissect-file-name filename) (tramp-file-local-name filename))))
	  (when former-buffer
	    (magit-refresh)
	    ;; The asynchronous git command completed, we can reclaim the process
	    ;; back.
	    (set-process-buffer process former-buffer)
	    (set-process-filter process #'magit-boost-filter)
	    (process-put process 'magit-boost-buffer nil)
	    (setq magit-boost-cmd-state 'free)))))))

(defun magit-boost-process-cmd (cmd input destination)
  "Execute a shell command CMD."
  (let ((connection-type (unless (and input (string-search "\r" input))
			   'pty))
	(local-dir (tramp-file-name-localname
		    (tramp-dissect-file-name default-directory)))
	stdout ret )
    (with-magit-boost-buffer-create default-directory connection-type
      (magit-boost-submit-cmd local-dir cmd input destination)
      (let ((process (get-buffer-process (current-buffer))))
	(while (eq magit-boost-cmd-state 'running)
	  (accept-process-output process 1 nil t))
	(setf stdout (apply #'buffer-substring magit-boost-cmd-stdout))
	(when (and magit-boost-cmd-stderr
		   (listp destination) (stringp (cadr destination)))
	  (let ((err (apply #'buffer-substring magit-boost-cmd-stderr)))
	    (unless (string-empty-p err)
	      (write-region err nil (cadr destination)))))
	(unless magit-boost-debug
	  (erase-buffer))
	(setf ret magit-boost-cmd-ret)
	(magit-boost-cmd-init)))
    (when (and (numberp ret) (= ret 0) destination)
      (insert stdout))
    ret))

(defun magit-boost--process-git (destination args &optional input)
  "Execute a Git command via the Magit Bash process."
  (let ((git-args (flatten-list args))
	fun)
    (if (and (stringp (car git-args))
	     (setf fun (intern (concat "magit-boost-" (car git-args)
				       (cadr git-args))))
	     (functionp fun))
	(funcall fun default-directory)
      (cl-flet* ((escape-quotes (str)
		   (replace-regexp-in-string "\"" "\\\\\"" str))
		 (format-arg (arg)
		   (format "\"%s\"" (escape-quotes arg))))
	(let ((cmd (mapconcat #'format-arg
			      (magit-process-git-arguments args)
			      " ")))
	(magit-boost-process-cmd (concat "git " cmd) input destination))))))

(defun magit-boost-load-files-attributes (files)
  "Batch load and cache Tramp file attributes for FILES.

This function constructs a single shell command to retrieve file
existence, readability, writability, directory status, symlink status,
and truenames for all provided FILES.  The results are parsed and
injected directly into the Tramp property cache.

This significantly improves performance on slow networks by replacing
multiple synchronous remote calls with a single batch execution."
  (let* ((test-and-props '(("-e" . "file-exists-p")
			   ("-f" . "file-regular-p")
			   ("-r" . "file-readable-p")
			   ("-w" . "file-writable-p")
			   ("-d" . "file-directory-p")
			   ("-L" . "file-symlink-p")))
	 (tests (mapconcat (lambda (tp)
			     (format "[ %s \"$file\" ] && echo -n ' %s'"
				     (car tp) (cdr tp)))
			   test-and-props " ; "))
	 (truename "[ -e \"$file\" ] && echo '' && readlink -nf \"$file\"")
	 (attributes (format "[ -e \"$file\" ] && echo '' && %s"
			     (with-parsed-tramp-file-name default-directory nil
			       (replace-regexp-in-string
				"\$1" "$file"
				(tramp-expand-script v tramp-stat-file-attributes)))))
	 (content (concat "[ -f \"$file\" ] && [ -r \"$file\" ] && echo '' && "
			  "cat \"$file\" && echo -n MAGIT_BOOST_EOF"))
	 (cmd (concat "for file in "
		      (mapconcat (lambda (file)
				   (format "'%s' "
					   (tramp-file-name-localname
					    (tramp-dissect-file-name file))))
				 files " ")
		      "; do " tests " ; " truename " ; " attributes " ; " content
		      "; echo ''"
		      "; done")))
    (with-temp-buffer
      (let ((ret (magit-boost-process-cmd cmd nil t)))
	(unless (= ret 0)
	  (error "Failed to read files attributes")))
      (goto-char (point-min))
      (dolist (file files)
	(let ((res (split-string (buffer-substring
				  (line-beginning-position) (line-end-position)))))
	  (dolist (tp test-and-props)
	    (let ((localname (tramp-file-name-localname (tramp-dissect-file-name file))))
	      (with-parsed-tramp-file-name file nil
		(tramp-set-file-property
		 v localname (cdr tp) (if (member (cdr tp) res) t nil)))))
	  (with-parsed-tramp-file-name file nil
	    (when (member "file-exists-p" res)
	      (forward-line)
	      (tramp-set-file-property
	       v localname "file-truename"
	       (buffer-substring (line-beginning-position) (line-end-position)))
	      (forward-line)
	      (cl-letf (((symbol-function 'tramp-get-file-property)
			 (lambda (key file property &optional default)
			   default)))
		(tramp-convert-file-attributes v localname 'integer
		  (read (buffer-substring (line-beginning-position)
					  (line-end-position)))))
	      (forward-line))
	    (when (and (member "file-regular-p" res)
		       (member "file-readable-p" res))
	      (forward-line)
	      (let ((start (point)))
		(when (search-forward "MAGIT_BOOST_EOF" nil t)
		  (with-parsed-tramp-file-name file f
		    (tramp-set-file-property
		     v f-localname "file-content"
		     (buffer-substring start (match-beginning 0))))))))
	  (forward-line))))))

(defun magit-boost-get-file-property (orig-fun &rest args)
  (let* ((key (car args))
	 (localname (cadr args))
	 (filename (tramp-make-tramp-file-name key localname)))
    (with-magit-boost-buffer filename 'pty
      (when (magit-boost-in-git-dir filename)
	(let ((property (caddr args))
	      (default (cadddr args)))
	  (add-to-list 'magit-boost-git-tree-files filename)
	  (let ((value (funcall orig-fun key localname property tramp-cache-undefined)))
	    (when (eq value tramp-cache-undefined)
	      (magit-boost-load-files-attributes magit-boost-git-tree-files))))))
    (apply orig-fun args)))

(defun magit-boost-tramp-sh-handle-file-writable-p (orig-fun &rest args)
  "Advice for `tramp-sh-handle-file-writable-p' to batch load file"
  (let* ((filename (car args)))
    (if (and (magit-boost-buffer filename 'pty) (magit-boost-in-git-dir filename))
	(with-parsed-tramp-file-name filename nil
	  (tramp-get-file-property v localname "file-writable-p"))
      (apply orig-fun args))))

(defun magit-boost-rev-parse--show-cdup (dir)
  (when-let ((cdup (with-magit-boost-buffer dir 'pty
		     (if (magit-boost-in-git-dir dir)
			 ""
		       (let ((sub (substring dir (length default-directory))))
			 (if (string-empty-p sub)
			     ""
			   (mapconcat (lambda (_) "..")
				      (cl-delete "" (split-string sub "/"))
				      "/")))))))
    (insert cdup "\n")
    0))

(defun magit-boost-rev-parse--show-toplevel (dir)
  (when-let* ((root (with-magit-boost-buffer-create dir 'pty
		      default-directory)))
    (with-parsed-tramp-file-name root r
      (insert r-localname)
      0)))

(defun magit-boost-rev-parse--git-dir (dir)
  (insert (with-magit-boost-buffer dir 'pty
	    magit-boost-git-dir))
  0)

(defun magit-boost-insert-file-contents (orig-fun &rest args)
  (let* ((filename (expand-file-name (car args)))
	 (visit (cadr args))
	 (buffer (current-buffer))
	 (content (when (cl-every #'null (cddr args))
		    (with-magit-boost-buffer filename 'pty
		      (when (magit-boost-in-git-dir filename)
			(with-parsed-tramp-file-name filename nil
			  (save-excursion
			    (tramp-get-file-property v localname "file-content"))))))))
    (if content
	(progn
	  (save-excursion
	    (insert content))
	  (when visit
	    (setq buffer-file-name filename
		  buffer-read-only (not (file-writable-p filename)))
	    (set-visited-file-modtime)
	    (set-buffer-modified-p nil))
	  (list filename (1- (point-max))))
      (apply orig-fun args))))

(defun magit-boost-process-git (orig-fun &rest args)
  "Advice for `magit-process-git' to optionally route Git commands through
a persistent Bash process.

If the current buffer's `default-directory' is a TRAMP (remote), execute
the Git command using Magit Boost's accelerated Bash process, passing
DESTINATION (the first element of ARGS) and the prepared Git arguments."
  (if-let ((ret (and (tramp-tramp-file-p default-directory)
		     (magit-boost--process-git (car args) (cdr args)))))
      ret
    (apply orig-fun args)))

(defun magit-boost-run-git-with-input (orig-fun &rest args)
  "Advice for `magit-run-git-with-input' to accelerate Git commands via a
persistent Bash process.

If the current buffer is not a TRAMP (remote) buffer, call the original
function `orig-fun' with ARGS.

Otherwise, execute the Git command using the Magit Bash process, piping
the current buffer's contents as input to the command. This reduces
process startup overhead and improves performance for repeated Git
operations."
  (if-let ((ret (and (tramp-tramp-file-p default-directory)
		     (magit-boost--process-git t args (buffer-string)))))
      ret
    (apply orig-fun args)))

(defun magit-boost-vc-responsible-backend (orig-fun &rest args)
  "If a Magit Boost buffer with a 'pty connection exists, return Git.

This avoids invoking the slower default implementation of
`vc-responsible-backend', which can be a performance bottleneck in Magit."
  (if (magit-boost-buffer (car args) 'pty)
      "Git"
    (apply orig-fun args)))

(defun magit-boost-handle-vc-registered (orig-fun file)
  (if (magit-boost-buffer file 'pty)
      '(Git)
    (funcall orig-fun file)))

(defun magit-boost-start-file-process (orig-fun name buffer program &rest args)
  (if (string= program "git")
      (let ((local-dir (tramp-file-name-localname
		    (tramp-dissect-file-name default-directory))))
	(with-magit-boost-buffer default-directory 'pty
	  (when (file-remote-p default-directory)
	    (unless (equal program "env")
	      (push program args)
	      (setq program "env"))
	    (cl-flet* ((escape-quotes (str)
			 (replace-regexp-in-string "'" "'\"'\"'" str))
		       (format-arg (arg)
			 (format "'%s'" (escape-quotes arg))))
	      (let ((cmd (format "%s=%s %s"  with-editor--envvar
				 (format-arg with-editor-sleeping-editor)
				 (mapconcat #'format-arg args " ")))
		    (process (get-buffer-process (current-buffer))))
		(set-process-filter process #'with-editor-process-filter)
		(magit-boost-submit-cmd local-dir cmd nil nil)
		(when (member "rebase" args)
		  (setq magit-boost-cmd-flush-file-properties t))
		;; Magit is going to appropriate the buffer, we need to store
		;; the original buffer to reclaim the process later.
		(process-put process 'magit-boost-buffer (current-buffer))
		(process-put process 'default-dir default-directory)
		(accept-process-output process 1 nil t)
		process)))))
    (apply orig-fun name buffer program args)))

(defun magit-boost-with-editor-process-filter (orig-fun &rest args)
  (funcall #'magit-boost-filter (car args) (cadr args))
  (apply orig-fun args))

(define-minor-mode magit-boost-mode
  "Minor mode to accelerate Magit Git commands by routing them through a
persistent Bash process."
  :init-value nil
  :global t
  :lighter " MBoost"
  (let ((advices '((magit-process-git . magit-boost-process-git)
		   (magit-run-git-with-input . magit-boost-run-git-with-input)
		   (vc-responsible-backend . magit-boost-vc-responsible-backend)
		   (tramp-get-file-property . magit-boost-get-file-property)
		   (tramp-sh-handle-file-writable-p . magit-boost-tramp-sh-handle-file-writable-p)
		   (insert-file-contents . magit-boost-insert-file-contents)
		   (with-editor-process-filter . magit-boost-with-editor-process-filter)
		   (start-file-process . magit-boost-start-file-process)
		   (tramp-sh-handle-vc-registered . magit-boost-handle-vc-registered))))
  (if magit-boost-mode
      (dolist (advice advices)
	(advice-add (car advice) :around (cdr advice)))
    (dolist (advice advices)
      (advice-remove (car advice) (cdr advice))))))

(defcustom magit-boost-progress-entry-points
  '(magit-status magit-refresh magit-checkout magit-rebase
    magit-rebase-interactive-1 magit-log-setup-buffer)
  "List of Magit commands that trigger the progress reporter."
  :type '(repeat symbol))

(defvar magit-boost-progress nil
  "The active progress reporter object updated during Git command
execution.")

(defun magit-boost-progress-init ()
  "Initialize the progress reporter and return its display name."
  (let ((name (propertize (if magit-boost-mode "Magit-Boost" "Magit")
			  'face 'font-lock-function-call-face)))
    (setq magit-boost-progress (make-progress-reporter (format "%s..." name)))
    name))

(defun magit-boost-git-command-progress (orig-fun &rest args)
  "Report Git command execution time."
  (let* ((flatten-args (flatten-list (cdr args)))
	 (cmd (mapconcat #'identity (nconc (list "git") flatten-args) " "))
	 (suffix (propertize (concat cmd "...") 'face 'font-lock-string-face))
	 (start-time (current-time)))
    (unless magit-boost-progress
      (magit-boost-progress-init))
    (progress-reporter-update magit-boost-progress suffix)
    (let ((ret (apply orig-fun args))
	  (duration (float-time (time-subtract (current-time) start-time))))
      (setf suffix (format "%s done, took %.02fs" suffix duration))
      (progress-reporter-update magit-boost-progress nil suffix)
      ret)))

(defun magit-boost-entry-progress (orig-fun &rest args)
  "Wrap Magit entry points to report total execution time."
  (let* ((name (magit-boost-progress-init))
	 (start-time (current-time))
	 (ret (apply orig-fun args))
	 (duration (float-time (time-subtract (current-time) start-time))))
    (message (format "%s... done, took %.02fs" name duration))
    ret))

(define-minor-mode magit-boost-progress-mode
  "Minor mode to display progress and performance timing for Magit
commands.

When enabled, this mode provides visual feedback in the echo area
during Git operations. It uses a progress reporter to show the
currently executing Git command and logs the total execution time
of major Magit entry points (defined in
`magit-boost-progress-entry-points`) once they complete.

This is particularly useful on slow networks to confirm that Magit is
actively working and to measure the performance benefits provided by
Magit Boost."
  :init-value nil
  :global t
  (if magit-boost-progress-mode
      (progn
	(advice-add 'magit-process-git
		    :around #'magit-boost-git-command-progress)
	(dolist (entry magit-boost-progress-entry-points)
	  (advice-add entry :around #'magit-boost-entry-progress)))
    (advice-remove 'magit-process-git
		   #'magit-boost-git-command-progress)
    (dolist (entry magit-boost-progress-entry-points)
      (advice-remove entry #'magit-boost-entry-progress))))

(provide 'magit-boost)
